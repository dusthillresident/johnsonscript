variable i;
stringvar text;
variable pixc;

if _argc
 if 0
  option "seedrnd" get_arg 0 get_arg 1 get_arg 2
  goto ".start"
 endif
 if = _argc 1
  option "seedrnd" val$$0
  goto ".start"
 endif
 if = _argc 2
  option "seedrnd" val$$0 val$$1
  goto ".start"
 endif
 if >= _argc 3
  option "seedrnd" val$$0 val$$1 val$$2
  goto ".start"
 endif
endif

.start

startgraphics 640 480;

drawmode 7
wait 64

while < i 1671
 caseof rnd 8
 when 0; # gcol
  if & rnd 0 1
   gcol rnd 0
  else
   gcol rnd 0 rnd 0 rnd 0
  endif
 when 1; # plotting mode
  drawmode rand_select 1 2 4 6 7 9 10 11 13 14
 when 2; # circle
  if & rnd 0 1
   circle rnd winw rnd winh >> rnd winh 1
  else
   circlef rnd winw rnd winh >> rnd winh 1
  endif
 when 3; # rectangle
  if & rnd 0 1
   if & rnd 0 1
    rectangle rnd winw rnd winh rnd winw rnd winh
   else
    rectangle rnd winw rnd winh rnd winh
   endif
  else
   if & rnd 0 1
    rectanglef rnd winw rnd winh rnd winw rnd winh
   else
    rectanglef rnd winw rnd winh rnd winh
   endif
  endif
 when 4; # triangle
  triangle rnd winw rnd winh rnd winw rnd winh rnd winw rnd winh 
 when 5; # line
  line rnd winw rnd winh rnd winw rnd winh 
 when 6; # text
  rand_string @text;
  drawtext rnd winw rnd winh rnd 0 text
 when 7; # pixel
  set pixc rnd 32
  while pixc
   pixel rnd winw rnd winh
   set pixc - pixc 1
  endwhile
 endcase
 set i + i 1
endwhile

while 1
 wait 100
endwhile

quit

function rand_string s local c i;
 set $s ""
 set c rnd 256
 set i 0;
 while < i c
  set $s cat$ $s chr$ | (& rnd 0 32) + asc$"A" rnd 26
  set i + i 1
 endwhile
endfunction

function rand_select ...;
return P rnd _num_params

function get_arg n;
 if && >= n 0 < n _argc
  return val$ $ n
 endif
return 0
